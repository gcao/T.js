<!DOCTYPE html>
<html>
<head>
  <meta http-equiv="content-type" content="text/html; charset=UTF-8">
  <title>T.js example - a TODO list with pagination</title>
  <style>
    .pagination .header a, .pagination .header span,
    .pagination .footer a, .pagination .footer span{
      margin-right: 10px;
    }
  </style>
  <script type="text/javascript" src="coffee-script.js"></script>
  <script type="text/javascript" src="watch.js"></script>
  <script type="text/javascript" src="../javascripts/jquery-1.8.2.min.js"></script>
  <script type="text/javascript" src="../javascripts/t.js"></script>
  <script type="text/coffeescript">

templates =
  main: (todos) ->
    [ "div#container"
      style:
        width: 300
        padding: 3
        border: "1px solid grey"

      [ "div.header"
        style:
          padding: 3
          background: "#fbb"
          font_weight: "bold"

        "My TODO List"
      ]
      [ "div.todos"
        style: width: "100%"

        T(templates.todos, todos)
      ]
      [ "input"
        type: "text"
        placeholder: "Type content and hit Enter"
        style: width: "98%"
        keyup: (event) ->
          return if event.which isnt 13

          event.preventDefault()
          todos.push $(this).val()
          $(this).val('')
      ]
    ]

  todo: (todo, index, todos) ->
    [ "div.content"
      style: padding: 3

      T.escape(todo)
      "&nbsp;&nbsp;"
      [ "a"
        href: "javascript:void(0)"
        click: -> todos.splice(index, 1)

        "X"
      ]
    ]

  todos: (todos) ->
    if todos.length > 0
      result = T(templates.pagination, todos, rowTemplate: templates.todo)
      #console.log JSON.stringify result
      result

  pagination: (data, config = {}) ->
    items    = data.length
    pageSize = config.pageSize || 5
    pages    = (items + pageSize - 1) / pageSize
    page     = config.page || 0

    if pages is 1
      return (T(config.rowTemplate, item, i, data) for item, i in data)

    createClickHandler = (page) ->
      -> $('.pagination').trigger('jumpToPage', page)

    links = ->
      for i in [0..pages - 1]
        if i is page
          [ 'span'
            i + 1
          ]
        else if i is 0 or i is pages - 1 or page < i < page + 3 or page - 3 < i < page
          [ 'a'
            href: 'javascript:void(0)'
            click: createClickHandler(i)
            i + 1
          ]
        else if (i is 1 and i < page - 3) or (i is pages - 2 and i < page + 3)
          '...'

    [ 'div.pagination'
      jumpToPage: (_, page) ->
        result = T(templates.pagination, window.todos, rowTemplate: templates.todo, page: page)
        console.log page
        console.log JSON.stringify result
        result.render replace: '.pagination'

      [ 'div.header', links() ]
      [ 'div.body'
        for i in [0..pageSize-1]
          index = i + page * pageSize
          if index >= items then continue

          item = data[index]
          T(config.rowTemplate, item, i, data)
      ]
      [ 'div.footer', links() ]
    ]

# Model
window.todos = ['Do 1', 'Do 2', 'Do 3', 'Do 4', 'Do 5', 'Do 6']

# Model change listener
watch window, 'todos', ->
  T(templates.todos, window.todos).render inside: '.todos'

output = T(templates.main, todos)
#console.log JSON.stringify(output.tags)
output.render inside: 'body'

</script>
</head>
<body>
T.js example 2

This example shows a simple TODO list.
</body>
</html>

