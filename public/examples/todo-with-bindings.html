<!DOCTYPE html>
<html>
<head>
  <meta http-equiv="content-type" content="text/html; charset=UTF-8">
  <title>T.js example 3 - a TODO list that uses data-binding</title>
  <script type="text/javascript" src="coffee-script.js"></script>
  <script type="text/javascript" src="watch.js"></script>
  <script type="text/javascript" src="../javascripts/jquery-1.8.2.min.js"></script>
  <script type="text/javascript" src="../javascripts/t.js"></script>
  <script type="text/coffeescript">

templates =
  main: (todos) ->
    [ "div#container"
      style:
        width: 300
        padding: 3
        border: "1px solid grey"

      [ "div.header"
        style:
          padding: 3
          background: "#fbb"
          font_weight: "bold"

        "My TODO List"
      ]
      [ "div.todos"
        style: width: "100%"

        templates.todos todos
      ]
      [ "input"
        type: "text"
        placeholder: "Type content and hit Enter"
        style: width: "98%"
        keyup: (event) ->
          return if event.which isnt 13

          event.preventDefault()
          todos.push $(this).val()
          $(this).val('')
      ]
    ]

  todo: (todos, index) ->
    [ "div.content"
      style: padding: 3

      T.escape(todos[index])
      "&nbsp;&nbsp;"
      [ "a"
        href: "javascript:void(0)"
        click: -> todos.splice(index, 1)

        "X"
      ]
    ]

  todos: (todos) ->
    if todos.length > 0
      templates.todo(todos, i) for i in [0..todos.length - 1]

# Model
window.todos = ['Do this', 'Do that']

# Model change listener
watch window, 'todos', ->
  T(templates.todos(window.todos)).render inside: '.todos'

T(templates.main(todos)).render inside: 'body'

</script>
</head>
<body>
T.js example 2

This example shows a simple TODO list.
</body>
</html>

