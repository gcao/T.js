<!DOCTYPE html>
<html>
<head>
  <meta http-equiv="content-type" content="text/html; charset=UTF-8">
  <title>T.js example 3 - a TODO list that uses data-binding</title>
  <script type="text/javascript" src="coffee-script.js"></script>
  <script type="text/javascript" src="watch.js"></script>
  <script type="text/javascript" src="../javascripts/jquery-1.8.2.min.js"></script>
  <script type="text/javascript" src="../javascripts/t.js"></script>
  <script type="text/coffeescript">

T.def 'main', (data) ->
  [ "div#container"
    style: 
      width: "300px"
      padding: "3px"
      border: "1px solid grey"

    [ "div.header"
      style:
        padding: "3px"
        background: "#fbb"
        "font-weight": "bold"
      "My TODO List"
    ]
    [ "div.todos"
      style: width: "100%"
      T('todos', data.todos) if data.todos?.length > 0
    ]
    [ "input"
      type: "text"
      placeholder: "Type content and hit Enter"
      style: width: "98%"
      keyup: (event) ->
        return if event.which isnt 13

        event.preventDefault()
        data.todos.push $(this).val()
        $(this).val('')
    ]
  ]

T.def 'todo', (todos, index) ->
  [ "div.content"
    style: padding: "3px"

    T.escape(todos[index])
    "&nbsp;&nbsp;"
    [ "a"
      href: "javascript:void(0)"
      click: -> todos.splice(index, 1)
      "X"
    ]
  ]

T.def 'todos', (todos) ->
  T('todo', todos, i) for i in [0..todos.length - 1]

window.data = todos: []

T('main', data).render inside: 'body'

watch window.data, 'todos', (name, oldValue, newValue) ->
  console.log data.todos
  T('todos', data.todos).render inside: '.todos'

data.todos.push 'Do this'
data.todos.push 'Do that'

</script>
</head>
<body>
T.js example 2

This example shows a simple TODO list.
</body>
</html>

