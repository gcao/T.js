<!DOCTYPE html>
<html>
<head>
  <meta http-equiv="content-type" content="text/html; charset=UTF-8">
  <title>T.js example 2 - a simple TODO list</title>
  <script type="text/javascript" src="coffee-script.js"></script>
  <script type="text/javascript" src="../javascripts/jquery-1.8.2.min.js"></script>
  <script type="text/javascript" src="../javascripts/t.js"></script>
  <script type="text/coffeescript">

T.def 'main', ->
  [ "div#container"
    renderComplete: ->
      updateContents()
    style: 
      width: "300px"
      padding: "3px"
      border: "1px solid grey"

    [ "div.header"
      style:
        padding: "3px"
        background: "#fbb"
        "font-weight": "bold"
      "My TODO List"
    ]
    [ "div.contents"
      style: width: "100%"
    ]
    [ "input"
      type: "text"
      style: width: "98%"
      keyup: (event) ->
        if event.which is 13
          event.preventDefault()
          contents.push $(this).val()
          $(this).val('')
          updateContents()
      placeholder: "Type content and hit Enter"
    ]
  ]

T.def 'rowTemplate', (item, index) ->
  [ "div.content"
    style: padding: "3px"

    T.escape(item)
    "&nbsp;&nbsp;"
    [ "a"
      href: "javascript:void(0)"
      click: ->
        contents.splice(index, 1)
        updateContents()
      "X"
    ]
  ]

T.def 'rowsTemplate', (rows) ->
  T('rowTemplate', item, i) for item, i in rows

contents = ["Do this", "Do that"]

updateContents = ->
  T('rowsTemplate').renderWith contents, 
    renderer: (s) -> $('.contents').html(s)

T('main').renderWith renderer: (s) -> $('body').html(s)

</script>
</head>
<body>
T.js example 2

This example shows a simple TODO list.
</body>
</html>

