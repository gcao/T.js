<!DOCTYPE html>
<html>
<head>
  <meta http-equiv="content-type" content="text/html; charset=UTF-8">
  <title>T.js example 2 - a simple TODO list</title>
  <script type="text/javascript" src="coffee-script.js"></script>
  <script type="text/javascript" src="../javascripts/jquery-1.8.2.min.js"></script>
  <script type="text/javascript" src="../javascripts/t.js"></script>
  <script type="text/coffeescript">

T.def 'todoContainer', (todos) ->
  [ "div#container"
    style: 
      width: "300px"
      padding: "3px"
      border: "1px solid grey"

    # Event handler
    todosUpdated: (e, newTodos) ->
      T('todos', newTodos).render inside: '.todos'

    [ "div.header"
      style:
        padding: "3px"
        background: "#fbb"
        "font-weight": "bold"
      "My TODO List"
    ]
    [ "div.todos"
      style: width: "100%"
      T('todos', todos)
    ]
    T('todoInput', todos)
  ]

T.def 'todo', (todos, index) ->
  [ "div.todo"
    style: padding: "3px"

    T.escape(todos[index])
    "&nbsp;&nbsp;"
    [ "a"
      href: "javascript:void(0)"
      click: ->
        todos.splice(index, 1)
        $(this).trigger('todosUpdated', [todos])
      "X"
    ]
  ]

T.def 'todos', (todos) ->
  return if todos.length is 0

  T('todo', todos, i) for i in [0..todos.length - 1]

T.def 'todoInput', (todos) ->
  [ "input"
    type: "text"
    style: width: "98%"
    keyup: (event) ->
      if event.which is 13
        event.preventDefault()

        todos.push $(this).val()
        $(this).val('')
        $(this).trigger('todosUpdated', [todos])
    placeholder: "Enter content and hit Return"
  ]

T('todoContainer', ["Do this", "Do that"]).render inside: 'body'

</script>
</head>
<body>
T.js example 2

This example shows a simple TODO list.
</body>
</html>

