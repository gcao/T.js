<!DOCTYPE html>
<html>
<head>
  <meta http-equiv="content-type" content="text/html; charset=UTF-8">
  <title>T.js example - an accordion widget</title>
  <link href="bootstrap-3.0.3.min.css" rel="stylesheet" type="text/css" media="screen"/>
  <script type="text/javascript" src="coffee-script.js"></script>
  <script type="text/javascript" src="../javascripts/jquery-1.8.2.min.js"></script>
  <script type="text/javascript" src="watch.js"></script>
  <script type="text/javascript" src="../javascripts/t.js"></script>
  <script type="text/coffeescript">



#http://getbootstrap.com/javascript/#collapse

setElement = (o) ->
  afterRender: (el) -> o.el = el

watchAndRun = (obj, prop, callback) ->
  callback()
  watch obj, prop, callback

bind = (el, obj, properties) ->
  tagName = $(el).get(0).tagName

  watch obj, properties, ->
    if tagName is 'INPUT'
      $(el).val(obj[properties])

  if tagName is 'INPUT'
    callback = ->
      delete obj.errors
      obj[properties] = $(this).val()

    $(el).blur(callback).change(callback)

AccordionGroup = (args...) ->
  @push arg for arg in args
  return  # Seems if the function returns an array, the object created with "new AccordionGroup" won't be able to find custom methods

AccordionGroup.prototype = new Array

AccordionGroup.prototype.collapseAllExcept = (accordion) ->
  for item in @
    if item isnt accordion
      return if item.active and not item.collapse()

  true

AccordionGroup.prototype.template = ->
  [ '.accordion-group'
    for accordion in @
      accordion.template()
  ]

class Accordion
  constructor: (@parent, @heading, @body) ->
    @parent.push @
    @disabled = true
    @finished = false

  enable: ->
    @disabled = false

  next: ->
    @parent[@parent.indexOf(@) + 1]

  expand: ->
    return if @disabled

    if @parent.collapseAllExcept(@)
      @active = true
      @next()?.enable()

  collapse: ->
    return if @section and not @section.isValid()

    @active = false

    true

  toggle: =>
    if @active then @collapse() else @expand()

  icon: ->
    if @disabled
      'ban-circle'
    else if @active
      'edit'
    else if @section?.isValid()
      'ok'
    else
      'play'

  template: ->
    [ '.accordion.panel.panel-default'
      setElement(@)
      #afterRender: (el) => @el = el
      [ '.panel-heading'
        [ 'h4.panel-title'
          [ 'a.accordion-toggle'

            href: 'javascript:void(0)'
            click: @toggle

            @heading()

            [ 'i.pull-right.glyphicon'
              afterRender: (el) =>
                watchAndRun @, ['active', 'disabled', 'finished'], =>
                  $(el).attr('class', "pull-right glyphicon glyphicon-#{@icon()}")
            ]
          ]
        ]
      ]
      [ '.panel-collapse.collapse'

        afterRender: (el) =>
          watchAndRun @, 'active', =>
            $(el).toggleClass('in', !!@active)

        [ '.panel-body'
          @body()
        ]
      ]
    ]

class Section
  constructor: (@name, @model) ->

  isValid: ->
    @model.validate()

  addToAccordion: (parent) ->
    @accordion = new Accordion(parent)
    @accordion.section = @
    @accordion.heading = => @name
    @accordion.body    = => @body()

class AccountSection extends Section
  constructor: (model) ->
    super('Account', model)

  body: ->
    return unless @model

    [ '.account'
      'Please fill in your contact information:'
      [ 'div'
        [ 'label', style: margin_right: 10, 'First name' ]
        [ 'input'
          name: 'firstName'
          value: @model.firstName or ''
          afterRender: (el) => bind(el, @model, 'firstName')
        ]
      ]
    ]

class AddressSection extends Section
  constructor: (model) ->
    super('Address', model)

  body: ->
    return unless @model

    [ '.address'
      'Please fill in your address:'
      [ 'div'
        [ 'label', style: margin_right: 10, 'Address line 1' ]
        [ 'input'
          name: 'addressLine1'
          value: @model.addressLine1 or ''
          afterRender: (el) => bind(el, @model, 'addressLine1')
        ]
      ]
    ]

class BillingSection extends Section
  constructor: (model) ->
    super('Billing', model)

  body: ->
    return unless @model

    [ '.billing'
      'Please fill in your billing information:'
      [ 'div'
        [ 'label', style: margin_right: 10, 'Credit card number:' ]
        [ 'input'
          name: 'cardNumber'
          value: @model.cardNumber or ''
          afterRender: (el) => bind(el, @model, 'cardNumber')
        ]
      ]
    ]

class ShippingSection extends Section
  constructor: (model) ->
    super('Shipping', model)

  body: ->
    return unless @model

    [ '.billing'
      'Please fill in your shipping information:'
      [ 'div'
        [ 'label', style: margin_right: 10, 'Shipping method:' ]
        [ 'input'
          name: 'shippingMethod'
          value: @model.shippingMethod or ''
          afterRender: (el) => bind(el, @model, 'shippingMethod')
        ]
      ]
    ]

class BaseModel
  constructor: ->

  validate: ->
    @errors = {}
    for validation in @constructor.validations
      validation(@)
    T.internal.isEmpty(@errors)

  isValid: ->
    @validate() unless @errors
    T.internal.isEmpty(@errors)

class Account extends BaseModel
  constructor: ->

  @validations: []
  @validations.push (model) ->
    unless model.firstName and model.firstName isnt ''
      myErrors = model.errors.firstName || []
      myErrors.push
        field: 'firstName'
        message: 'First name is required'
      model.errors.firstName = myErrors

class Address extends BaseModel
  constructor: ->

  @validations: []
  @validations.push (model) ->

class Billing extends BaseModel
  constructor: ->

  @validations: []
  @validations.push (model) ->

class Shipping extends BaseModel
  constructor: ->

  @validations: []
  @validations.push (model) ->

window.accordionGroup = new AccordionGroup

new AccountSection(new Account).addToAccordion(accordionGroup)
new AddressSection(new Address).addToAccordion(accordionGroup)
new BillingSection(new Billing).addToAccordion(accordionGroup)
new ShippingSection(new Shipping).addToAccordion(accordionGroup)

accordionGroup[0].enable()
accordionGroup[1].enable()
accordionGroup[0].expand()

T(accordionGroup.template()).render inside: 'body'



  </script>
</head>
<body>
This example shows a simple accordion widget created with T.js.
</body>
</html>

