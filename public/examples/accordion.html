<!DOCTYPE html>
<html>
<head>
  <meta http-equiv="content-type" content="text/html; charset=UTF-8">
  <title>T.js example - an accordion widget</title>
  <link href="bootstrap-3.0.3.min.css" rel="stylesheet" type="text/css" media="screen"/>
  <style>
    .collapse {
      display: none;
    }
    .collapse.in {
      display: block;
    }
  </style>
  <script type="text/javascript" src="coffee-script.js"></script>
  <script type="text/javascript" src="../javascripts/jquery-1.8.2.min.js"></script>
  <script type="text/javascript" src="watch.js"></script>
  <script type="text/javascript" src="../javascripts/t.js"></script>
  <script type="text/coffeescript">



#http://getbootstrap.com/javascript/#collapse

bind = (el, obj, properties) ->
  tagName = $(el).get(0).tagName

  watch obj, properties, ->
    if tagName is 'INPUT'
      $(el).val(obj[properties])

  if tagName is 'INPUT'
    callback = ->
      delete obj.errors
      obj[properties] = $(this).val()

    $(el).blur(callback).change(callback)

class window.AccordionGroup
  constructor: (@data...) ->

  add: (args...) ->
    @data.push arg for arg in args

  get: (index) -> @data[index]

  indexOf: (child) -> @data.indexOf(child)

  collapseAllExcept: (accordion) ->
    for item in @data
      if item isnt accordion
        return if item.active and not item.collapse()

    true

  template: ->
    [ '.accordion-group'
      for accordion in @data
        accordion.template()
    ]

class Accordion
  constructor: (@parent, @heading, @body) ->
    @parent.add @
    @disabled = true
    @finished = false

  enable: ->
    @disabled = false

  next: ->
    @parent.get @parent.indexOf(@) + 1

  expand: ->
    return if @disabled

    if @parent.collapseAllExcept(@)
      @active = true
      @next()?.enable()

  collapse: ->
    return if @section and not @section.isValid()

    @active = false

    true

  toggle: ->
    if @active then @collapse() else @expand()

  icon: ->
    if @disabled
      'ban-circle'
    else if @active
      'edit'
    else if @section?.isValid()
      'ok'
    else
      'play'

  template: ->
    [ '.accordion.panel.panel-default'
      afterRender: (el) => @el = el
      [ '.panel-heading'
        [ 'h4.panel-title'
          [ 'a.accordion-toggle'

            href: 'javascript:void(0)'
            click: => @toggle()

            @heading()

            [ 'i.pull-right.glyphicon'
              afterRender: (el) =>
                callback = =>
                  $(el).attr('class', "pull-right glyphicon glyphicon-#{@icon()}")

                watch @, ['active', 'disabled', 'finished'], callback

                callback()
            ]
          ]
        ]
      ]
      [ '.panel-collapse.collapse'

        afterRender: (el) =>
          callback = => $(el).toggleClass('in', !!@active)

          watch @, 'active', callback

          callback()

        [ '.panel-body'
          @body()
        ]
      ]
    ]

class Section
  constructor: (@name) ->
    @validations = []

  validate: ->
    @errors = {}
    for validation in @validations
      validation(@)

  isValid: ->
    if not @errors then @validate()

    T.internal.isEmpty(@errors)

  addToAccordion: (parent) ->
    @accordion = new Accordion(parent)
    @accordion.section = @
    @accordion.heading = => @name
    @accordion.body    = => @body()

window.accordionGroup = new AccordionGroup()

window.account = new Section('Account')
account.validations.push (model) ->
  if model.firstName is null or model.firstName is '' or typeof model.firstName is 'undefined'
    model.errors.firstName = "First name is required"

account.body = ->
  [ '.account'
    'Please fill in your contact information:'
    [ 'div'
      [ 'label', style: margin_right: 10, 'First name' ]
      [ 'input'
        name: 'firstName'
        value: @firstName or ''
        afterRender: (el) => bind(el, @, 'firstName')
      ]
    ]
  ]

account.addToAccordion(accordionGroup)

window.address = new Section('Address')
address.validations.push (model) ->
  if model.addressLine1 is null or model.addressLine1 is '' or typeof model.addressLine1 is 'undefined'
    model.errors.addressLine1 = "Address line 1 is required"

address.body = ->
  [ '.address'
    'Please fill in your home address:'
    [ 'div'
      [ 'label', style: margin_right: 10, 'Address line 1' ]
      [ 'input'
        name: 'addressLine1'
        value: @addressLine1 or ''
        afterRender: (el) => bind(el, @, 'addressLine1')
      ]
    ]
  ]

address.addToAccordion(accordionGroup)

window.billing = new Section('Billing')
billing.validations.push (model) ->
  if model.cardNumber is null or model.cardNumber is '' or typeof model.cardNumber is 'undefined'
    model.errors.cardNumber = "Credit card number is required"

billing.body = ->
  [ '.billing'
    'Please fill in your payment method:'
    [ 'div'
      [ 'label', style: margin_right: 10, 'Credit card number' ]
      [ 'input'
        name: 'cardNumber'
        value: @cardNumber or ''
        afterRender: (el) => bind(el, @, 'cardNumber')
      ]
    ]
  ]

billing.addToAccordion(accordionGroup)

window.shipping = new Section('Shipping')
shipping.validations.push (model) ->
  if model.addressLine1 is null or model.addressLine1 is '' or typeof model.addressLine1 is 'undefined'
    model.errors.addressLine1 = "Address line 1 is required"

shipping.body = ->
  [ '.shipping'
    'Please fill in your shipping address:'
    [ 'div'
      [ 'label', style: margin_right: 10, 'Address line 1' ]
      [ 'input'
        name: 'addressLine1'
        value: @addressLine1 or ''
        afterRender: (el) => bind(el, @, 'addressLine1')
      ]
    ]
  ]

shipping.addToAccordion(accordionGroup)

accordionGroup.get(0).enable()
accordionGroup.get(1).enable()
accordionGroup.get(0).expand()

T(accordionGroup.template()).render inside: 'body'



  </script>
</head>
<body>
This example shows a simple accordion widget created with T.js.
</body>
</html>

